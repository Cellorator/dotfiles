#+title: My Emacs Config
#+author: Cellorator
#+property: header-args :tangle "./init.el"
#+auto_tangle: t

* Package Manager
** elpaca
*** Install
#+begin_src emacs-lisp :tangle ./early-init.el
(setq package-enable-at-startup nil)
#+end_src

#+begin_src emacs-lisp
(defvar elpaca-installer-version 0.11)
(defvar elpaca-directory (expand-file-name "elpaca/" user-emacs-directory))
(defvar elpaca-builds-directory (expand-file-name "builds/" elpaca-directory))
(defvar elpaca-repos-directory (expand-file-name "repos/" elpaca-directory))
(defvar elpaca-order '(elpaca :repo "https://github.com/progfolio/elpaca.git"
                              :ref nil :depth 1 :inherit ignore
                              :files (:defaults "elpaca-test.el" (:exclude "extensions"))
                              :build (:not elpaca--activate-package)))
(let* ((repo  (expand-file-name "elpaca/" elpaca-repos-directory))
       (build (expand-file-name "elpaca/" elpaca-builds-directory))
       (order (cdr elpaca-order))
       (default-directory repo))
  (add-to-list 'load-path (if (file-exists-p build) build repo))
  (unless (file-exists-p repo)
    (make-directory repo t)
    (when (<= emacs-major-version 28) (require 'subr-x))
    (condition-case-unless-debug err
        (if-let* ((buffer (pop-to-buffer-same-window "*elpaca-bootstrap*"))
                  ((zerop (apply #'call-process `("git" nil ,buffer t "clone"
                                                  ,@(when-let* ((depth (plist-get order :depth)))
                                                      (list (format "--depth=%d" depth) "--no-single-branch"))
                                                  ,(plist-get order :repo) ,repo))))
                  ((zerop (call-process "git" nil buffer t "checkout"
                                        (or (plist-get order :ref) "--"))))
                  (emacs (concat invocation-directory invocation-name))
                  ((zerop (call-process emacs nil buffer nil "-Q" "-L" "." "--batch"
                                        "--eval" "(byte-recompile-directory \".\" 0 'force)")))
                  ((require 'elpaca))
                  ((elpaca-generate-autoloads "elpaca" repo)))
            (progn (message "%s" (buffer-string)) (kill-buffer buffer))
          (error "%s" (with-current-buffer buffer (buffer-string))))
      ((error) (warn "%s" err) (delete-directory repo 'recursive))))
  (unless (require 'elpaca-autoloads nil t)
    (require 'elpaca)
    (elpaca-generate-autoloads "elpaca" repo)
    (let ((load-source-file-function nil)) (load "./elpaca-autoloads"))))
(add-hook 'after-init-hook #'elpaca-process-queues)
(elpaca `(,@elpaca-order))
#+end_src
*** Options
#+begin_src emacs-lisp
;; Enable integration with use-package function, remove once all converted to elpaca
(elpaca elpaca-use-package
  (elpaca-use-package-mode))
(setq elpaca-use-package-by-default t
      use-package-always-ensure t)
#+end_src

* Options
** Main
#+begin_src emacs-lisp
(setq inhibit-startup-message t) ; Don't show splash screen

(setq frame-resize-pixelwise t) ; Remove weird gaps at bottom and right edges

(menu-bar-mode -1) ; Disable menu bar
(tool-bar-mode -1) ; Disable tool bar
(scroll-bar-mode -1) ; Disable scroll bar

(column-number-mode) ; Display column number on mode bar
(global-display-line-numbers-mode 1) ; Display line numbers

(setq default-font "Hurmit Nerd Font")
(setq monospace-font default-font)
;; (setq variable-width-font "Metropolis")
(set-face-attribute 'default nil :family default-font :height 105)
(set-face-attribute 'fixed-pitch nil :family monospace-font :height 105)

;; (set-face-attribute 'variable-pitch nil :family variable-width-font :height 1.2)

(global-visual-line-mode) ; Enable line wrap

(savehist-mode) ; Save minibuffer history
(global-auto-revert-mode) ; Automatically update chaged buffers

;; Disable backups and autosaves
(setq make-backup-files nil)
(setq auto-save-default nil)

(setq tab-width 4)
(setq-default indent-tabs-mode nil) ;; Use spaces instead of tabs

(setq-default show-trailing-whitespace t) ;; Highligh trailing spaces
#+end_src

* Keybinds
** Setup
#+begin_src emacs-lisp
;; More convenient keybind setting
(use-package general
  :ensure (:wait t)
  :config (general-evil-setup t))

;; Show custom keybind hints
(use-package which-key
  :custom
  (which-key-add-column-padding 3)
  :config (which-key-mode))

;; Emulate vim keybindings
(use-package evil
  :init
  (setq evil-want-keybinding nil) ; So evil-collection doesn't yell at me
  (setq evil-want-C-i-jump nil) ; Make TAB work normally (auto-indent)
  (setq evil-respect-visual-line-mode t)  ; Make j and k move between wrapped lines
  (setq evil-undo-system 'undo-fu)
  (setq evil-want-fine-undo t)
  :config (evil-mode 1))

(use-package evil-collection
  :config (evil-collection-init)
  :after evil)

(use-package evil-org
  :after org
  :hook org-mode
  :config
  (require 'evil-org-agenda)
  (evil-org-agenda-set-keys))

;; Undo and redo
(use-package undo-fu)
(use-package undo-fu-session
  :config (undo-fu-session-global-mode))
#+end_src

** Basics
Setting the leader key and some basic commands for navigating emacs and the filesystem

#+begin_src emacs-lisp
;; Set leader key
(general-create-definer <leader>
  :states '(normal insert visual emacs)
  :keymaps 'override
  :prefix "SPC"
  :global-prefix "C-SPC")

;; Copy  paste
(<leader>
  "y" '(clipboard-kill-ring-save :wk "Copy to clipboard")
  "p" '(clipboard-yank :wk "Paste from clipboard"))

;; Filesystem
(<leader>
  "f" '(:ignore t :wk "Find")
  "ff" '(find-file :wk "Find file")
  "fd" '(dired :wk "Find directory (dired)"))

;; Buffers
(<leader>
  "b" '(:ignore t :wk "Buffers")
  "bb" '(consult-buffer :wk "Switch to buffer")
  "bj" '(next-buffer :wk "Next buffer")
  "bk" '(previous-buffer :wk "Previous buffer")
  "br" '(revert-buffer :wk "Reload changes to buffer")
  "bw" '(kill-this-buffer :wk "Kill current buffer"))

;; LSP
(<leader>
 "l" '(:ignore t :wk "LSP")
 "lr" '(lsp-rename :wk "Rename symbol"))

;; Reload
(defun reload-config()
  (interactive)
  (load-file user-init-file))
(<leader>
  "rr" '(reload-config :wk "Reload configuration")
  "re" '(restart-emacs :wk "Restart Emacs"))
#+end_src

* Packages
** Tools
Useful thingies

*** Completion

#+begin_src emacs-lisp
  ;; A completion-style for space separated completion
  (use-package orderless
    :custom
    (completion-styles '(orderless partial-completion basic))
    (completion-category-defaults nil)
    (completion-category-overrides '((file (styles partial-completion)))))

  ;; Completion UI
  (use-package vertico
    :init (vertico-mode))

  (use-package consult
    :hook
    (minibuffer-setup . (lambda ()
                          (setq completion-in-region-function
                                #'consult-completion-in-region))))

  ;; Buffer completion
  (use-package corfu
    :custom
    (corfu-auto t)
    (corfu-cycle t)
    (global-corfu-minibuffer nil)
    (corfu-on-exact-match nil)
    :init
    (global-corfu-mode))

  ;; Hopefully fixes error when trying to autocomplete in text-mode
  (setopt text-mode-ispell-word-completion nil)
  (defun my-dabbrev-in-text()
    (add-to-list 'completion-at-point-functions #'cape-dabbrev))
  (add-hook 'text-mode-hook #'my-dabbrev-in-text)

  (use-package cape
    :init
    (add-hook 'completion-at-point-functions #'cape-keyword)
    (add-hook 'completion-at-point-functions #'cape-dabbrev)
    (add-hook 'completion-at-point-functions #'cape-file)
    (add-hook 'completion-at-point-functions #'cape-elisp-block))

  ;; Annotations in completion UI
  (use-package marginalia
    :init (marginalia-mode))
#+end_src
*** Snippets
#+begin_src emacs-lisp
(use-package yasnippet
  :config (yas-global-mode 1))
#+end_src
*** Lsp
#+begin_src emacs-lisp
(use-package lsp-mode
  :custom
  (lsp-completion-provider :none) ;; we use Corfu!
  (lsp-response-timeout 1) ;; Hopefully stops random hanging
  ;; Performance optimizations
  (gc-cons-threshold 1000000000)
  (read-process-output-max (* 1024 1024))
  :init
  (defun my/lsp-mode-setup-completion ()
    (setf (alist-get 'styles (alist-get 'lsp-capf completion-category-defaults))
          '(orderless))) ;; Configure orderless
  :hook
  ((lsp-completion-mode . my/lsp-mode-setup-completion)
   (csharp-ts-mode . lsp-deferred)
   (python-ts-mode . (lambda ()
                       (require 'lsp-pyright)
                       (lsp-deferred))))) ; or lsp-deferred
#+end_src
**** Lsp Booster
#+begin_src emacs-lisp :tangle ./early-init.el
(setenv "LSP_USE_PLISTS" "true")
#+end_src

#+begin_src emacs-lisp
(defun lsp-booster--advice-json-parse (old-fn &rest args)
  "Try to parse bytecode instead of json."
  (or
   (when (equal (following-char) ?#)
     (let ((bytecode (read (current-buffer))))
       (when (byte-code-function-p bytecode)
         (funcall bytecode))))
   (apply old-fn args)))
(advice-add (if (progn (require 'json)
                       (fboundp 'json-parse-buffer))
                'json-parse-buffer
              'json-read)
            :around
            #'lsp-booster--advice-json-parse)

(defun lsp-booster--advice-final-command (old-fn cmd &optional test?)
  "Prepend emacs-lsp-booster command to lsp CMD."
  (let ((orig-result (funcall old-fn cmd test?)))
    (if (and (not test?)                             ;; for check lsp-server-present?
             (not (file-remote-p default-directory)) ;; see lsp-resolve-final-command, it would add extra shell wrapper
             lsp-use-plists
             (not (functionp 'json-rpc-connection))  ;; native json-rpc
             (executable-find "emacs-lsp-booster"))
        (progn
          (when-let ((command-from-exec-path (executable-find (car orig-result))))  ;; resolve command from exec-path (in case not found in $PATH)
            (setcar orig-result command-from-exec-path))
          (message "Using emacs-lsp-booster for %s!" orig-result)
          (cons "emacs-lsp-booster" orig-result))
      orig-result)))
(advice-add 'lsp-resolve-final-command :around #'lsp-booster--advice-final-command)
#+end_src
*** Treesittter
#+begin_src emacs-lisp
(use-package treesit-auto
  :custom
  (treesit-auto-install 'prompt)
  (treesit-font-lock-level 4)
  :config
  (treesit-auto-add-to-auto-mode-alist 'all)
  (global-treesit-auto-mode))
#+end_src
*** Languages
#+begin_src emacs-lisp
(use-package lua-mode)
(use-package lsp-pyright
  :custom (lsp-pyright-langserver-command "basedpyright")) ;; or basedpyright
#+end_src
*** Misc
#+begin_src emacs-lisp
(use-package transient)
;; Cool git front-end
(use-package magit
  :general
  (<leader>
    "g" '(magit :wk "Open Magit")))
#+end_src

** QOL
Some small quality of life stuff

#+begin_src emacs-lisp
;; Automatically set indentation per filetype
(use-package dtrt-indent
  :config (dtrt-indent-global-mode))

(use-package smartparens
  :config
  (smartparens-global-mode)
  (require 'smartparens-config))

(use-package evil-commentary
  :config (evil-commentary-mode))

(use-package restart-emacs)

(use-package frames-only-mode
  :init (frames-only-mode))
#+end_src

** UI
*** Theme
 #+begin_src emacs-lisp
   (use-package color-theme-sanityinc-tomorrow)
   (use-package doom-themes)
   (use-package kanagawa-themes
     :config (load-theme 'kanagawa-dragon t))
#+end_src

*** telephone-line
A nice mode-line
**** Install
#+begin_src emacs-lisp
  (use-package telephone-line
    :ensure (:wait t))
#+end_src

**** Custom Symbols
#+begin_src emacs-lisp
(defvar telephone-line-circle-right
  (make-instance 'telephone-line-unicode-separator
                 :char #xe0b6
                 :inverse-video nil))
(defvar telephone-line-circle-left
  (make-instance 'telephone-line-unicode-separator
                 :char #xe0b4))
(defvar telephone-line-slash-right
    (make-instance 'telephone-line-unicode-separator
                   :char #xe0bd
                   :inverse-video nil))
(defvar telephone-line-slash-left
  (make-instance 'telephone-line-unicode-separator
                 :char #xe0b9))
#+end_src

**** Configuration
#+begin_src emacs-lisp
(setq telephone-line-primary-right-separator 'telephone-line-circle-right)
(setq telephone-line-primary-left-separator 'telephone-line-circle-left)
(setq telephone-line-secondary-right-separator 'telephone-line-slash-right)
(setq telephone-line-secondary-left-separator 'telephone-line-slash-left)

(setq telephone-line-subseparator-faces
      '((evil . evil)
        (accent . accent)
        (nil . nil)))

(setq telephone-line-lhs
      '((evil   . (telephone-line-evil-tag-segment))
        (accent . (telephone-line-buffer-name-segment
                   telephone-line-vc-segment
                   telephone-line-process-segment))
        (nil    . ())))
(setq telephone-line-rhs
      '((nil    . (telephone-line-misc-info-segment))
        (accent . (telephone-line-major-mode-segment))
        (evil   . (telephone-line-airline-position-segment))))

(telephone-line-mode 1)
#+end_src

** Note-Taking
*** org
**** Options
#+begin_src emacs-lisp
(use-package org
  :ensure (:wait t)
  :custom
  (org-startup-indented t) ; Indent heading  levels
  (org-startup-folded 'show2levels)
  (org-ellipsis "ï‘¼")
  (org-src-tab-acts-natively t) ; Make tab work in code blocks
  (org-src-preserve-indentation t) ; Stop annoying indentation when making a new line in code blocks
  (org-cycle-separator-lines -1) ; Don't fold empty lines between headings
  (org-log-into-drawer "LOGBOOK") ; Put logging into drawer instead of plain text
  :hook
  (org-mode . (lambda () (display-line-numbers-mode -1)))) ;; Remove line numbers

;; Stop org heading tab-folding from opening all subtrees
(add-hook 'org-cycle-hook
          (lambda (state)
            (when (eq state 'children)
              (setq org-cycle-subtree-status 'subtree))))
#+end_src
**** org-latex
#+begin_src emacs-lisp
(setq org-latex-packages-alist
      '(("" "esdiff")
        ("" "esvect")
        ("" "tikz")
        ("" "tikz-cd")))
(setq org-latex-create-formula-image-program 'dvisvgm) ; Makes tikz preview work
(setq org-preview-latex-image-directory (concat user-emacs-directory "cache/org-latex/"))
#+end_src
**** org-agenda
#+begin_src emacs-lisp
(setq org-agenda-files '("~/notes/inbox/"))
(setq org-agenda-todo-ignore-scheduled 'future)
#+end_src
**** org-capture
#+begin_src emacs-lisp
(setq org-capture-templates
      '(("t" "TODO")
        ("tt" "Unscheduled" entry
         (file+headline "~/notes/inbox/inbox.org" "Unscheduled")
         "* TODO %?")
        ("ts" "Scheduled" entry
         (file+headline "~/notes/inbox/inbox.org" "Scheduled")
         "* TODO %?\nSCHEDULED: %^T")
        ("n" "Note" entry
         (file+headline "~/notes/inbox/inbox.org" "Notes")
         "* %?")))
#+end_src
**** org-babel
#+begin_src emacs-lisp
(setq org-babel-no-eval-on-ctrl-c-ctrl-c nil)
(setq org-confirm-babel-evaluate nil)

(org-babel-do-load-languages
 'org-babel-load-languages
 '((emacs-lisp . t)
   (org . t)
   (latex . t)
   (python . t)))
#+end_src
**** Keybinds
#+begin_src emacs-lisp
(<leader>
  "o" '(:ignore t :wk "org-mode")
  "ole" '(org-latex-export-to-pdf :wk "Export to latex pdf")
  "or" '(org-babel-execute-src-block :wk "Execute code block"))
(general-nmap "RET" 'org-open-at-point)
(general-def :states '(normal insert)
  "C-M-<return>" '(lambda ()
                    (interactive)
                    (org-insert-heading-respect-content)
                    (org-do-demote)
                    (evil-append 1)))
#+end_src
**** Theming
#+begin_src emacs-lisp
(setq org-startup-with-latex-preview t)
;; Resize Org headings
(dolist (face '((org-level-1 . 1.5)
                (org-level-2 . 1.35)
                (org-level-3 . 1.25)
                (org-level-4 . 1.2)
                (org-level-5 . 1.2)
                (org-level-6 . 1.2)
                (org-level-7 . 1.2)
                (org-level-8 . 1.2)))
  (set-face-attribute (car face) nil :font monospace-font :weight 'bold :height (cdr face)))
;; Make the document title a bit bigger
(set-face-attribute 'org-document-title nil :font monospace-font :weight
                    'bold :height 1.5)
(plist-put org-format-latex-options :scale 0.45) ; Make latex preview bigger
(setf (cdr (assoc 'file org-link-frame-setup)) 'find-file) ; Open files in same window
#+end_src
**** Visuals
#+begin_src emacs-lisp
;; Replace text with cool symbols
(use-package org-modern
  :custom
  (org-modern-star 'replace)
  (org-modern-keyword nil)
  :hook (org-mode))

;; Make stuff dissapear and stuff
(use-package org-appear
  :custom
  (org-hide-emphasis-markers t) ; Hide bold and italic markup
  :hook org-mode
  :after org)

;; Preview latex in editor
(use-package org-fragtog
  :hook (org-mode org-roam-mode)
  :after org)
#+end_src
**** Extra Packages
#+begin_src emacs-lisp
;; For tangling configuration file on save
(use-package org-auto-tangle
  :defer t
  :hook org-mode
  :after org)
#+end_src

*** org-roam
Knowledge management system for taking notes

**** Installation
#+begin_src emacs-lisp
  (use-package org-roam
    :ensure (:wait t)
    :after org)
#+end_src
**** Options
#+begin_src emacs-lisp
(setq org-roam-directory (file-truename "~/notes"))
(org-roam-db-autosync-mode)
(add-to-list 'display-buffer-alist
             '("\\*org-roam\\*"
               (display-buffer-in-direction)
               (direction . right)
               (window-width . 0.33)
               (window-height . fit-window-to-buffer)))
(setq org-roam-node-display-template "${hierarchy:*}")
(setq org-roam-completion-everywhere t)
#+end_src
**** Templates
#+begin_src emacs-lisp
(setq org-roam-capture-templates
      '(("i" "main note" plain "%?"
         :target (file+head
                  "main/%<%Y%m%dT%H%M%S>--${slug}.org"
                  "#+title: ${title}\n#+date: [%<%Y-%m-%d %a %H:%M>]\n#+filetags:")
         :immediate-finish t
         :unnarrowed t)))
#+end_src
**** Override Sluggification Function
Use "-" instead of "_"

#+begin_src emacs-lisp
(require 'ucs-normalize)
(cl-defmethod org-roam-node-slug ((node org-roam-node))
  "Return the slug of NODE."
  (let ((title (org-roam-node-title node))
        (slug-trim-chars '(;; Combining Diacritical Marks https://www.unicode.org/charts/PDF/U0300.pdf
                           768 ; U+0300 COMBINING GRAVE ACCENT
                           769 ; U+0301 COMBINING ACUTE ACCENT
                           770 ; U+0302 COMBINING CIRCUMFLEX ACCENT
                           771 ; U+0303 COMBINING TILDE
                           772 ; U+0304 COMBINING MACRON
                           774 ; U+0306 COMBINING BREVE
                           775 ; U+0307 COMBINING DOT ABOVE
                           776 ; U+0308 COMBINING DIAERESIS
                           777 ; U+0309 COMBINING HOOK ABOVE
                           778 ; U+030A COMBINING RING ABOVE
                           780 ; U+030C COMBINING CARON
                           795 ; U+031B COMBINING HORN
                           803 ; U+0323 COMBINING DOT BELOW
                           804 ; U+0324 COMBINING DIAERESIS BELOW
                           805 ; U+0325 COMBINING RING BELOW
                           807 ; U+0327 COMBINING CEDILLA
                           813 ; U+032D COMBINING CIRCUMFLEX ACCENT BELOW
                           814 ; U+032E COMBINING BREVE BELOW
                           816 ; U+0330 COMBINING TILDE BELOW
                           817 ; U+0331 COMBINING MACRON BELOW
                           )))
    (cl-flet* ((nonspacing-mark-p (char)
                                  (memq char slug-trim-chars))
               (strip-nonspacing-marks (s)
                                       (ucs-normalize-NFC-string
                                        (apply #'string (seq-remove #'nonspacing-mark-p
                                                                    (ucs-normalize-NFD-string s)))))
               (cl-replace (title pair)
                           (replace-regexp-in-string (car pair) (cdr pair) title)))
      (let* ((pairs `(("[^[:alnum:][:digit:]]" . "-") ;; convert anything not alphanumeric
                      ("--*" . "-")                   ;; remove sequential underscores
                      ("^-" . "")                     ;; remove starting underscore
                      ("-$" . "")))                   ;; remove ending underscore
             (slug (-reduce-from #'cl-replace (strip-nonspacing-marks title) pairs)))
        (downcase slug)))))

#+end_src
**** Show Node Hierarchy in Search
#+begin_src emacs-lisp
(cl-defmethod org-roam-node-hierarchy ((node org-roam-node))
  (let ((level (org-roam-node-level node)))
    (concat
     (when (> level 0) (concat (org-roam-node-file-title node) " > "))
     (when (> level 1) (concat (string-join (org-roam-node-olp node) " > ") " > "))
     (org-roam-node-title node))))
#+end_src

*** org-node
#+begin_src emacs-lisp
(use-package org-mem
  :defer
  :config
  ;; At least one of these two is needed
  (setq org-mem-do-sync-with-org-id t)
  (setq org-mem-watch-dirs '("~/notes/")) ;; Configure me
  (org-mem-updater-mode))

(use-package org-node
  :config
  (org-node-backlink-mode)
  (org-node-cache-mode))
#+end_src

*** denote
Another knowledge management system, mostly used for its renaming functions
#+begin_src emacs-lisp
(use-package denote
  :custom
  (denote-directory (file-truename "~/notes/"))
  (denote-rename-confirmations nil)
  (denote-known-keywords '(math linearalgebra calculus physics history))
  (denote-date-prompt-use-org-read-date t) ; Use cool org calendar for setting dates
  :config
  ;; Remove denote id in front matter, set here because doesn't work in :custom
  (setq denote-org-front-matter
        "#+title: %1$s
,#+date: %2$s
,#+filetags: %3$s\n"))
#+end_src

*** Keybinds
#+begin_src emacs-lisp
(<leader>
  "nc" '(org-capture :wk "org-capture")
  "na" '(org-agenda :wk "org-agenda"))

(<leader>
  "n" '(:ignore t :wk "Notes")
  "nf" '(org-roam-node-find :wk "Find note")
  "ni" '(org-roam-node-insert :wk "Insert note")
  "nb" '(org-roam-buffer-toggle :wk "Open backlinks buffer"))

(<leader>
  "nm" '(:ignore t :wk "Modify note metadata (title, keywords, aliases, id)")
  "nmt" '(denote-rename-file-title :wk "Change title")
  "nmk" '(denote-rename-file-keywords :wk "Change keywords/filetags")
  "nma" '(org-roam-alias-add :wk "Add aliases")
  "nmi" '(org-id-get-create :wk "Create ID for file/headline")
  "nmm" '(denote-rename-file-using-front-matter :wk "Update filename from frontmatter")
  "nmn" '(denote-add-front-matter :wk "Regenerate fronmatter from filename"))
#+end_src
